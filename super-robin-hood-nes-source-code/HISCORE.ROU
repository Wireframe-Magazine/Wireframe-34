
	if hiscore=1
	
****************************************
*       high score sort routine	*
****************************************

;To use:-
;set up a highscore table
;eg maddr+$20,$20,14,"PETE A   12345"
;   maddr+$20,$40,14,"PETE B    1234"
;   maddr+$20,$60,14,"PETE C     123"
;   maddr+$20,$80,14,"PETE D      12"
;   maddr+$20,$a0,14,"PETE E       1"
;set SIZEOFMESSAGE to equal the length of each line (17 in the above)
;set NUMBERENTRIES to equal the number of names in the table (5 in the above)
;set SIZEOFSCORE to equal the length of the score string (eg "00000"=5)
;set SIZEOFNAME to equal the length of the name string (eg "peter1234"=9)
;set NAMETRAILNO to equal the no. of control bytes before the actual name

sizeofmessage equ 17	;size of one line in table
			;eg maddr+?,?,14,"peter....12345"=17 bytes long
numberentries equ 10	;number of names in the table (5 in the above)
sizeofscore	 equ 7	;length of score string "00000"=5
sizeofname	 equ 9	;length of name string "PETER1234"=9
nametrailno	 equ 0	;the name is stored with maddr data before it
			;eg maddr+?,?,length,"peter1234". This no. equals
			;the no. of trailing bytes (3 in above)
namenumafter	equ 1	;the number of bytes after the score

highscores	equ hiscoresinram

tryhiscore	move.val highscores+numberentries*sizeofmessage-sizeofscore-namenumafter,address
	lda #0
	sta hipos
	ldx #numberentries
aas	ldy #0
ias2	lda (address),y
	cmp #32		;if table has leading zeros these 3
	bne sdf		;lines are not needed
	lda #33+0
sdf	cmp score,y
	beq inge2
	bcc hire2
	jmp enbl2
inge2	iny
	cpy #sizeofscore
	bne ias2
	jmp hire2
enbl2	jmp ovhi2
hire2	move.w address,address2
	inc hipos
lidy	add16toval address,-sizeofmessage,address
	dex
	bne aas
ovhi2	lda hipos
	bne och1
	jmp notahigh
och1	cmp #1
	beq nufr

	move.val highscores+(numberentries-1)*sizeofmessage+nametrailno,address
	ldx hipos
	dex
	stx temp
MOVV	add16toval address,-sizeofmessage,address3
	slddrind address3,address,sizeofmessage-nametrailno-namenumafter
	move.w address3,address
	dec temp
	bne movv
nufr
;;	jsr entername								;get the name

copyscoreacross
	ldy #0
!1	lda score,y
	sta (address2),y
	iny
	cpy #sizeofscore
	bne !1

copynameacross
	add16toval address2,-sizeofname,mappointer
	ldy #sizeofname-1
!1	lda name,y
	sta (mappointer),y
	dey
	bpl !1

	lda #numberentries
	sub hipos
	sta hipos
	rts

notahigh	lda #255	
	sta hipos
	rts
	;jmp displayhiscores				;print the highs

************************************************************************

namelen	equ temp9
letter	equ temp8


trytoentername
	ldxy clearscr
	jsr prtmessage


missleadingnoughts
	ldy #0
	sty temp		;indented=miss leading noughts
!1	lda score,y
	bit temp
	bmi !2
	cmp #0
	beq !3
	dec temp
	bne !2
!3	lda #30-33		
!2	sta score,y
	iny
	cpy #6
	bne !1

putincommer	ldx #5
	ldy #6
!2	lda score,x
	add #33
	sta score,y
	cpx #3
	bne !3
	dey
	lda #29
	sta score,y
!3	dey
	dex
	bpl !2

	jsr tryhiscore

displayhiscores

	jsr setuphiscores

;	bit hipos
;	bmi !1
	lda tune
	cmp #1
	beq !1
	lda #3
	jsr starttune
!1
	jsr turninteron
	lda #0
	sta namelen
	lda #1
	sta letter

lookingathiscores
	flybackvar
	bit hipos
	bmi !1
	jsr entername
	jmp !2
!1	ldx #&0d
	jsr pulsecolour
!2

	lda pad
	and #stmask
	beq lookingathiscores
	lda debounce
	and #stmask
	bne lookingathiscores
	
	jmp turninterofffade
;----------------------------------------------	

setuphiscores
	lda #hiscorechrs0.43
	jsr copyblockofcompactedchrs
	lda #hiscorechrs0.43s
	jsr copyblockofcompactedchrs
	lda #okchr0.1
	jsr copyblockofcompactedchrs

	ldxy hiscorescreen
	jsr prtmessage

	lda #100
	sta robiny
	lda hipos
	cmp #254
	beq showcredits
	ldxy hiscoremess
	jsr prtmessage
	lda hipos
	bmi !1
	ldxy enternamemess
	jmp printedentername
!1	ldxy hiscorepressstart
	jmp printedentername
showcredits	
	ldxy credits
printedentername
	jsr prtmessage

	jsr clearspriteblock

	ldxy hiscorescreencolours
	jmp setfade
;----------------------------------------------
entername	;jmp chooseletter
	lda counter
	and #7
	bne chooseletter
	lda pad
	and #umask+rmask
	beq !1
	inc letter
	lda #leftrightletterfx
	jsr soundfx
!1	lda pad
	and #dmask+lmask
	beq !2
	dec letter
	lda #leftrightletterfx
	jsr soundfx
!2	lda letter
	and #31
	sta letter
	

chooseletter	lda pad
	and #amask
	beq !1
	lda debounce
	and #amask
	bne !1
	lda letter
	bne !2
	lda #stmask
	sta pad
	jmp !1

!2
	lda namelen
	cmp #8
	beq !1
	lda letter
	jsr printachr
	lda letter
	ldy namelen
	sta (mappointer),y
	inc namelen
	lda #chooseletterfx
	jsr soundfx
	LDA LETTER
	jmp flashletter
!1

deleteletter	lda pad
	and #bmask
	beq !1
	lda debounce
	and #bmask
	bne !1
	ldy namelen
	beq !1
	dec namelen
	lda #30
	dey
	sta (mappointer),y
	jsr printachr
	lda #deleteletterfx
	jsr soundfx
	
!1

flashletter	lda #0
	sta temp
	lda namelen
	asl
	asl
	asl
	add #64
	tax
	lda hipos
	asl
	asl
	asl
	add #63+32
	tay
	lda letter
	jsr pokesprite
	ldx #&0e
	jsr pulsecolour
	ldx #&11
	jmp pulsecolour
;------------------------------------------
printachr	;a=chr to print
	tax
	move.val &2188-32,address
	ldy hipos
!1	add16toval address,32,address
	dey
	bpl !1
	ldy vrampointer
	lda address+1
	sta vrambuffer,y
	lda address
	add namelen
	sta vrambuffer+1,y
	lda #1
	sta vrambuffer+2,y
	txa
	sta vrambuffer+3,y
	tya
	add #4
	sta vrampointer
	rts
;----------------------------------------
hiscorescreen
	defb mgosub
	defw clearscr
	defb maddr+&20,&20
	HEX 2B2C00000032
	defb mdownline
	HEX 2D2E2F3033343536
	defb mloop,18,&3a,mendloop
	hex 3B3C3A3A3A3A
	HEX 202020313738
	defb mloop,20,&39,mendloop
	hex 3D3E39393939
	HEX 3F4041424E4F
	defb mloop,20,&50,mendloop
	hex 5C5D50505050
	HEX 4344454651525354
	defb mloop,18,&5b,mendloop
	hex 5E5F5B5B5B5B
	HEX 4748494A5556
	defb mloop,20,&57,mendloop
	hex 605563646500
	HEX 004B4C4D5859
	defb mloop,20,&5a,mendloop
	hex 616266676800
	defb maddr+&21,03,mloop,18
	defb 00,&69,&6a,mloop,20,&20,mendloop,mlen,2,&89,&8a,mdownline,mendloop

	defb mjump
	defw hiscorescr4

hiscorescr4	defb mlen,3
	HEX 009697
	defb mloop,20,mlen,1,&20,mendloop,mlen,2,&B4,&B5
	defb mdownline,mlen,3
	HEX 939899
	defb mloop,10,mlen,2,&9A,&9B,mendloop,mlen,3
	hex  B6B7B8
	defb mdownline,mlen,32-6
	HEX B9BBBCBDBEBDBEBDBEBDBEBDBEBDBEBDBEBDBEBDBEBDBEC2C3BA
	defb mdownline,mlen,32-9
	HEX 00BF00C0C1C0C1C0C1C0C1C0C1C0C1C0C1C0C1C0C1C0C1
	defb maddr+&23,&c0,mlen,8*8
	HEX 5050505050505050552505050505855500220000000088000022000000008800
	HEX 0022000000008800002200000000880000220000000088200002000000000802
	defb mend
hiscorefancybits
	defb mgosub
	defw hiscorebit
dofleur	defb maddr+&22,&e7,mgosub
	defw fleurdlys
	defb maddr+&22,&f7

fleurdlys	defb mlen,2,&8b,&8c,mdownline
	defb mlen,3,&9c,&9d,&9e,mdownline
	defb mlen,3,&9f,&a0,&a1,mdownline
	defb mlen,2,&a2,&a3,mend


hiscorebit	defb maddr+&21,&08
 hex 6b206c
 defb mdownline,mlen,16
 hex 6D6E6F70757677787D7E7F8085867576
 defb mdownline,mlen,16
 hex 71727374797A7B7C818283848788797A
 defb mend

hiscorepressstart
	defb maddr+&23,&0e
	cbm           "PRESS"
	cbm mdownline,"START"
	defb mjump
	defw flashingbit

enternamemess
	defb maddr+&23,&0a
	cbm           "PLEASE ENTER"
	cbm mdownline," YOUR NAME"
flashingbit	defb mgosub
	defw hiscorefancybits
	defb maddr+&23,&f2,mlen,4,&0c,&0f,&0f,&03
	defb mend


credits	defb maddr+&21,&07
	cbm "     CREDITS",mdownline,mdownline
	cbm "DESIGNED BY^^",mdownline
	cbm "  THE OLIVER TWINS",mdownline,mdownline
	cbm "GRAPHICS BY^^",mdownline
	cbm "        PAUL ADAMS",mdownline
	cbm "MUSIC FX BY^^",mdownline
	cbm "     GAVIN RAEBURN",mdownline
	cbm "LICENSED BY^^",mdownline
	cbm "   CODEMASTERS LTD",mdownline,mdownline
	cbm "OTHER GAMES BY",mdownline
	cbm "THE OLIVER TWINS^^",mdownline,mdownline
	cbm "THE F^ A^ OF DIZZY",mdownline
	cbm "  BMX SIMULATOR",mdownline
	cbm "     FIREHAWK",mdownline
	cbm "  GO",27," DIZZY GO",27
	defb mend


hiscorescreencolours
	HEX 21271609
	hex 2120270e
	hex 2127160e
	hex 21372709
	
	hex 21271109
	hex 2120150e
	hex 21000000
	hex 210e0e0e
hiscoremess	defb maddr+&21,&88,mgosub
	defw hiscoresinram
	defb mend


;----------------------------------------------
copyhiscorestoram
	cpx #&97
	beq dontcopyinscores
copyhis
	ldx #17*10+1
copyhiscores	lda initnames-1,x
	sta hiscoresinram-1,x
	dex
	bne copyhiscores
	rts
dontcopyinscores
	ldx #0
!1	ldy #0
!3	lda checkhiscores,y
	bpl !2
	cmp #131
	bcs !2

	cmp #128
	bne !5
	lda hiscoresinram,x
	cmp #33
	bcs copyhis
	jmp !4
!5
	lda hiscoresinram,x
	cmp #&1e
	beq !4
	cmp #32
	bcc copyhis
	cmp #43
	bcs copyhis
	jmp !4

!2	cmp hiscoresinram,x
	bne copyhis
!4	inx
	iny
	cpy #17
	bne !3
	cpx #10*17
	bne !1
	lda hiscoresinram+17*10
	cmp #255
	bne copyhis
	rts
;-----------------------------------------
initnames	cbm "FIRST^^^^",30,35,33,29,33,33,33,mdownline
	cbm "SECOND^^^",30,34,41,29,33,33,33,mdownline
	cbm "THIRD^^^^",30,34,39,29,33,33,33,mdownline
	cbm "FOURTH^^^",30,34,37,29,33,33,33,mdownline
	cbm "FIFTH^^^^",30,34,35,29,33,33,33,mdownline
	cbm "SIXTH^^^^",30,34,33,29,33,33,33,mdownline
	cbm "SEVENTH^^",30,30,41,29,33,33,33,mdownline
	cbm "EIGHTH^^^",30,30,39,29,33,33,33,mdownline
	cbm "NINTH^^^^",30,30,37,29,33,33,33,mdownline
	cbm "TENTH^^^^",30,30,35,29,33,33,33,mdownline,mend
name	cbm "^^^^^^^^^"
checkhiscores defs 9,128
	defb 129,129,129,29,129,129,129,mdownline



	endif

